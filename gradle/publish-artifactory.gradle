/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */

// Artifactory publishing configuration (Gradle 9.x compatible)
// This replaces the legacy publish.gradle that used older artifactory plugin API
// Requires: com.jfrog.artifactory plugin 5.x or later

ext.artifactoryUser = project.hasProperty('artifactoryUser') ? project.artifactoryUser : System.getenv('ARTIFACTORY_USER')
ext.artifactoryPassword = project.hasProperty('artifactoryPassword') ? project.artifactoryPassword : System.getenv('ARTIFACTORY_PASSWORD')

if (!artifactoryUser) {
    // try to read artifactory.properties
    def base = file(projectDir)
    def artifactoryFile = new File(base, 'artifactory.properties')
    while (!artifactoryFile.exists()) {
        base = base.parentFile
        if (!base) break
        artifactoryFile = new File(base, 'artifactory.properties')
    }
    if (artifactoryFile.exists()) {
        logger.lifecycle 'Found artifactory.properties in ' + base.path
        def props = new Properties()
        props.load(artifactoryFile.newReader())
        ext.artifactoryUser = props.getProperty('artifactoryUser','')
        ext.artifactoryPassword = props.getProperty('artifactoryPassword','')
    } else {
        logger.lifecycle 'No artifactory.properties file found'
    }
}

logger.lifecycle "ArtifactoryUser user: $artifactoryUser"

// Only configure artifactory if credentials are available
if (artifactoryUser && artifactoryPassword) {
    allprojects {
        if (project == rootProject || rootProject.ext.modules().contains(project)) {
            apply plugin: 'com.jfrog.artifactory'

            artifactory {
                contextUrl = project.hasProperty('artifactoryContext') ? project.artifactoryContext : 'https://groovy.jfrog.io/artifactory'
                publish {
                    repository {
                        repoKey = project.hasProperty('artifactoryRepoKey') ? project.artifactoryRepoKey : 'libs-snapshot-local'
                        username = rootProject.artifactoryUser
                        password = rootProject.artifactoryPassword
                    }
                    defaults {
                        // Publish the maven publication created in upload-publish.gradle
                        publications('maven')
                        publishArtifacts = true
                        publishPom = true
                    }
                }
            }
        }
    }

    // Root project also publishes groovy-all and groovy-bom
    artifactory {
        publish {
            defaults {
                publications('maven', 'groovyAll', 'groovyBom')
            }
        }
    }

    // Add backport publications
    rootProject.backports.each { pkg, classList ->
        rootProject.artifactory {
            publish {
                defaults {
                    publications("backport${pkg.capitalize()}")
                }
            }
        }
    }

    artifactoryPublish.dependsOn(['backportJars', 'publishToMavenLocal'])
} else {
    logger.lifecycle 'Artifactory publishing disabled - no credentials configured'
}
