/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */

/*
 * This file describes artifacts which will be distributed in separate jars, intended to be used with
 * <b>older</b> versions of Groovy for binary compatibility with classes compiled with newer versions of groovy
 *
 * Updated for Gradle 9.x compatibility (uses archiveBaseName/archiveClassifier instead of baseName/classifier)
 */

ext.backports = [
        compat23: ['org/codehaus/groovy/runtime/typehandling/ShortTypeHandling.class']
]

task backportJars {
    group = 'Backports'
    description = 'Generates backports jars'
}

dist.dependsOn backportJars

backports.each { pkg, classList ->
    def backportJar = task "backport${pkg}Jar"(type:Jar) {
        group = 'Backports'
        dependsOn jar

        from zipTree(jar.archiveFile)
        include classList
        archiveBaseName = "groovy-backports-$pkg"
        archiveClassifier = ''
    }

    // The following two jars are empty. Maven Central *requires* a javadoc and sources classifier
    // even when there's nothing to include
    def javadocJar = task "backport${pkg}JavadocJar"(type:Jar) {
        group = 'Backports'
        dependsOn jar

        archiveBaseName = "groovy-backports-$pkg"
        archiveClassifier = 'javadoc'
    }
    def sourcesJar = task "backport${pkg}SourcesJar"(type:Jar) {
        group = 'Backports'
        dependsOn jar

        archiveBaseName = "groovy-backports-$pkg"
        archiveClassifier = 'sources'
    }
    backportJars.dependsOn([backportJar, javadocJar, sourcesJar])
}
