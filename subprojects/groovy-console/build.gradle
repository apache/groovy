/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
plugins {
    id 'org.apache.groovy-library'
}

dependencies {
    api rootProject // AstBrowser has methods with Closure params...
    implementation "com.github.javaparser:javaparser-core:${versions.javaParser}"
    implementation "org.ow2.asm:asm-util:${versions.asm}"
    implementation "org.abego.treelayout:org.abego.treelayout.core:${versions.treelayout}"
    implementation projects.groovySwing
    implementation projects.groovyTemplates
    implementation ("org.apache.ivy:ivy:${versions.ivy}") {
        transitive = false
    }
    testImplementation projects.groovyTest
    testImplementation testFixtures(projects.groovySwing)

    groovyCompilerClasspath(project(":")) {
        capabilities {
            requireCapability 'org.apache.groovy:groovy-grapes'
        }
    }
}

tasks.register("console", JavaExec) {
    jvmArgs += ["-Dgroovy.attach.groovydoc=true"]

    mainClass = 'groovy.console.ui.Console'
    classpath = sourceSets.main.runtimeClasspath
}
