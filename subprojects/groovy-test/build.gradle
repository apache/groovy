/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
plugins {
    id 'org.apache.groovy-library'
}

dependencies {
    api rootProject   // GroovyTestCase uses Closure...
    api "junit:junit:${versions.junit}" // GroovyTestCase extends TestCase...
    // groovy-ant needed for FileNameFinder used in AllTestSuite and JavadocAssertionTestSuite
    testRuntimeOnly(project(':groovy-ant')) {
        transitive = false // bring in just what we need below
    }
    testRuntimeOnly "org.apache.ant:ant:${versions.ant}"
    testRuntimeOnly "org.apache.ant:ant-launcher:${versions.ant}"
}

groovyLibrary {
    withGrooid(
            "org.apache.groovy:groovy-test-grooid:${sharedConfiguration.groovyVersion.get()}",
            [] as Set,
            [:],
            [
                    '*': ['META-INF/NOTICE']
            ],
            ['META-INF/NOTICE', 'META-INF/INDEX.LIST'],
            [
                    (rootProject.file('notices/NOTICE-GROOIDJARJAR').absolutePath): 'META-INF/NOTICE'
            ]
    )
}

plugins.withId('eclipse') {
    eclipse.classpath.file.whenMerged {
        entries.removeAll { entry -> entry.path == '/groovy-ant' }
    }
}

apply from: "${rootProject.projectDir}/gradle/jacoco/jacocofix.gradle"
