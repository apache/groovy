/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
plugins {
    id 'org.apache.groovy-library'
}

configurations {
    jaxb
    jaxbRuntime
}

dependencies {
    implementation rootProject
    testImplementation project(':groovy-test')
    jaxb 'javax.xml.bind:jaxb-api:2.3.0'
    jaxbRuntime 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    jaxbRuntime 'com.sun.xml.bind:jaxb-impl:2.3.0.1'
    jaxbRuntime 'javax.activation:activation:1.1.1'
}

task moduleDescriptor(type: org.apache.groovy.gradle.WriteExtensionDescriptorTask) {
    extensionClasses = 'org.apache.groovy.jaxb.extensions.JaxbExtensions'
}
compileJava.dependsOn moduleDescriptor

if (JavaVersion.current().isJava9Compatible()) {
    tasks.withType(JavaCompile) {
        classpath += configurations.jaxb
    }
    tasks.withType(Javadoc) {
        classpath += configurations.jaxb
    }
    tasks.withType(Groovydoc) {
        classpath += configurations.jaxb
    }
    tasks.withType(GroovyCompile) {
        groovyClasspath += configurations.jaxb
        classpath += configurations.jaxb
    }
    tasks.withType(Test) {
        classpath += configurations.jaxb
        classpath += configurations.jaxbRuntime
    }
}
